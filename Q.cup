import java_cup.runtime.*;
import ast.*;
import java.util.*;
import java.lang.*;

parser code {:
    
    public void report_error(String message, Object info) {
   
               if (info instanceof java_cup.runtime.Symbol) {
                        java_cup.runtime.Symbol current = ((java_cup.runtime.Symbol) info);
               System.out.println("Parse error at line: "+(current.left+1)+" column: "+(current.right+1));                
               
            }
    }
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};

terminal SEMICOLON, COMMA, DOT, COLON;
terminal LEFTBRACE, RIGHTBRACE;
terminal LEFTBRACKET, RIGHTBRACKET;
terminal LEFTTBRACKET, RIGHTTBRACKET;
terminal LEFTPAREN, RIGHTPAREN;

terminal BOOL, INT, FLOAT, STRING, LIST, CHAR, TUPLE, VOID;
terminal Boolean BOOLLITERAL;
terminal Integer INTLITERAL; terminal Float FLOATLITERAL;
terminal java.lang.String STRINGLITERAL;
terminal Character CHARLITERAL;
terminal String ID;
terminal FDEF, TDEF;

terminal ASSIGN;
terminal NOT, OR, AND;
terminal PLUS, MINUS, MULTIPLY, DIVIDE, POWER;
terminal IN, LENGTH, CONCAT;
terminal LESSTHAN, LESSTHANEQUAL, GREATERTHAN, GREATERTHANEQUAL, EQUAL, NOTEQUAL;

terminal IF, ELSE, REPEAT, UNTIL, RETURN, DO, WHILE;

non terminal TypeNode type;
non terminal ExprNode expression;
non terminal FinalNode final;
non terminal TermNode term;
non terminal FactorNode factor;
non terminal ExponentNode exponent;
non terminal AccessorNode accessor;
non terminal SequenceNode sequence;
non terminal ListNode list;
non terminal TupleNode tuple;
non terminal ExprListNode expression_clist,expression_clist2;
non terminal VarDeclNode var_decl;
non terminal VarTypeNode var_type;
non terminal ExprListNode var_init;

non terminal TypeNode function_type;
non terminal FuncDeclNode function_decl;
non terminal ParamListNode parameter_clist, parameter_clist2;

non terminal DeclListNode global_decl_list, local_decl_list;
non terminal DeclNode global_decl;
non terminal DatatypeNode datatype_decl;

non terminal BlockNode block;
non terminal StatementListNode statement_list;
non terminal StmtNode statement;
non terminal IfElseNode ifelse;
non terminal RptUntilNode repeatuntil;
non terminal WhileStmtNode while;
non terminal AssignNode assignment;
non terminal FuncCallNode function_call;
non terminal ReturnStmtNode return;
non terminal ProgramNode program;

precedence right POWER;
precedence left DIVIDE, MULTIPLY;
precedence left PLUS, MINUS;

precedence nonassoc NOT;
precedence left OR, AND;
precedence right IN;
precedence nonassoc LESSTHAN, LESSTHANEQUAL, GREATERTHAN, GREATERTHANEQUAL, EQUAL, NOTEQUAL;

start with program;

type ::= BOOL:t {: RESULT = new TypeNode("bool");RESULT.setup(tleft,tright);:}
	| INT:t {: RESULT = new TypeNode("int");RESULT.setup(tleft,tright);:}
	| FLOAT:t {: RESULT = new TypeNode("float");RESULT.setup(tleft,tright);:}
	| STRING:t {: RESULT = new TypeNode("string");RESULT.setup(tleft,tright);:}
	| CHAR:t {: RESULT = new TypeNode("char");RESULT.setup(tleft,tright);:}
	| LIST:t {: RESULT = new TypeNode("list");RESULT.setup(tleft,tright);:}
	| TUPLE:t {: RESULT = new TypeNode("tuple");RESULT.setup(tleft,tright);:}
	| ID:t {: RESULT = new TypeNode("id");RESULT.setup(tleft,tright);:}
;

program ::= 
global_decl_list:glbdecl block:blk {: RESULT = new ProgramNode(blk,glbdecl);RESULT.setup(glbdeclleft,glbdeclright); :}
| block:blk {: RESULT=new ProgramNode(blk,null);RESULT.setup(blkleft,blkright); :}
| error  {:System.out.print("Error\n");:}
;

global_decl_list ::= 
global_decl_list:declist global_decl:v 
{: if (declist != null) {
                       declist.declarations.add(v);
                       RESULT = declist;
                       RESULT.setup(vleft,vright);
                       
                   } else {
                       
                       RESULT = new DeclListNode(v);
                       RESULT.setup(vleft,vright);
                   }
                :}

| global_decl:v
{:
                       RESULT = new DeclListNode(v);
                       RESULT.setup(vleft,vright);
                   :}
;

global_decl ::= 
datatype_decl:datadecl 		{: RESULT = datadecl;:}
| var_decl:vardecl			{: RESULT = vardecl;:}
| function_decl	:funcdecl		{: RESULT = funcdecl;:}
;

function_decl ::= 
FDEF ID:id LEFTPAREN parameter_clist:paramlist RIGHTPAREN COLON function_type:ftype block:blk	{: RESULT = new FuncDeclNode(paramlist,id,ftype,blk);RESULT.setup(idleft,idright);:}
;

var_type ::= 
ID:id COLON type:tp 	{: RESULT = new VarTypeNode(tp,id);RESULT.setup(idleft,idright);:}
; 

var_decl ::= 
var_type:vtype var_init:vinit SEMICOLON {: RESULT = new VarDeclNode(vinit,vtype);RESULT.setup(vtypeleft,vtyperight);:}
;

var_init ::=
ASSIGN expression_clist:elist	{: RESULT = elist;:}
|
;

datatype_decl ::=
TDEF ID:id COLON parameter_clist:parlist SEMICOLON	{: 
	//System.out.println("CUP: Tdef found");
	RESULT = new DatatypeNode(parlist,id);
	RESULT.setup(idleft,idright);:}
;

function_type ::=
VOID:v 		{: RESULT = new TypeNode("void");RESULT.setup(vleft,vright);:}
| type:t 	{: RESULT = t;:}
;

parameter_clist ::=
var_type:v parameter_clist2:parlist
{: if (parlist != null) {
                       parlist.params.add(v);
                       RESULT = new ParamListNode(parlist.params);
                       RESULT.setup(vleft,vright);
                       
                   } else {
                       ArrayList<VarTypeNode> res = new ArrayList<VarTypeNode>();
                       res.add(v);
                       RESULT = new ParamListNode(res);
                       RESULT.setup(vleft,vright);
                   }
                :}
| {: RESULT= null;:};

parameter_clist2 ::= 
COMMA var_type:v parameter_clist2:parlist
{: if (parlist != null) {
                       parlist.params.add(v);
                       RESULT = new ParamListNode(parlist.params);
                      RESULT.setup(vleft,vright);
                       
                   } else {
                       ArrayList<VarTypeNode> res = new ArrayList<VarTypeNode>();
                       res.add(v);
                       RESULT = new ParamListNode(res);
                       RESULT.setup(vleft,vright);
                   }
                :}
| {: RESULT = null;:};

block ::= 
LEFTBRACE local_decl_list:declist statement_list:stlist RIGHTBRACE {: RESULT = new BlockNode(stlist,declist);RESULT.setup(declistleft,declistright);:}
| LEFTBRACE statement_list:stlist RIGHTBRACE					{: RESULT = new BlockNode(stlist,null);RESULT.setup(stlistleft,stlistright);:}
;

// Local x only allows variable declarations
local_decl_list ::= 
local_decl_list:declist var_decl:v
{: if (declist != null) {
                       declist.declarations.add(v);
                       RESULT = declist;
                       RESULT.setup(declistleft,declistright);
                       
                   } else {
                       
                       RESULT = new DeclListNode(v);
                       RESULT.setup(vleft,vright);
                   }
                :}

| var_decl:v
{:
                       RESULT = new DeclListNode(v);
                       RESULT.setup(vleft,vright);
                   :}
;

statement_list ::=
statement:s statement_list:stlist
{: if (stlist != null) {
                       stlist.statements.add(s);
                       RESULT = new StatementListNode(stlist.statements);
                       RESULT.setup(sleft,sright);
                      
                   } else {
                       ArrayList<StmtNode> res = new ArrayList<StmtNode>();
                       res.add(s);
                       RESULT = new StatementListNode(res);
                       RESULT.setup(sleft,sright);
                   }
                :}
|{:RESULT = new StatementListNode(new ArrayList<StmtNode>());:};

statement ::= 
assignment:ass		{: RESULT = ass;:}
| function_call:func SEMICOLON	{: RESULT = new FuncCallStmtNode(func);RESULT.setup(funcleft,funcright);:}	
| ifelse:ifel		{: RESULT = ifel;:}
| while:whi		{: RESULT = whi;:}
| repeatuntil:rptuntil	{: RESULT = rptuntil;:}
| return:ret		{: RESULT = ret;:}
;

ifelse ::= 
IF LEFTPAREN expression:e RIGHTPAREN LEFTBRACE statement_list:stlist1 RIGHTBRACE ELSE LEFTBRACE statement_list:stlist2 RIGHTBRACE	{: RESULT = new IfElseNode(e,stlist1,stlist2);RESULT.setup(eleft,eright);:}
;

while ::=
WHILE LEFTPAREN expression:e RIGHTPAREN DO LEFTBRACE statement_list:stlist RIGHTBRACE	{: RESULT = new WhileStmtNode(e,stlist);RESULT.setup(eleft,eright);:}
;

repeatuntil ::= 
REPEAT LEFTBRACE statement_list:stlist RIGHTBRACE UNTIL LEFTPAREN expression:e RIGHTPAREN SEMICOLON		{: RESULT = new RptUntilNode(e,stlist);RESULT.setup(stlistleft,stlistright);:}
;

function_call ::=
ID:id LEFTPAREN expression_clist:elist RIGHTPAREN	{: RESULT = new FuncCallNode(elist,id);RESULT.setup(elistleft,elistright);:}
;

return ::= 
RETURN expression:e SEMICOLON 	{: RESULT = new ReturnStmtNode(e);RESULT.setup(eleft,eright);:}
| RETURN SEMICOLON		{: RESULT = new ReturnStmtNode(null);:}
;

assignment ::= 
accessor:acc ASSIGN expression:e SEMICOLON	{: RESULT = new AssignNode(e,acc);RESULT.setup(accleft,accright);:}
;

sequence ::=
STRINGLITERAL:s		{: 
					
					RESULT = new StringLitNode(s);RESULT.setup(sleft,sright);
					:}
| tuple	:tup		{: RESULT = tup;:}
| list:lis		{: RESULT = lis;:}
;

accessor ::=
ID:d {: 
                   RESULT = new AccessorNode(d);RESULT.setup(dleft,dright); :}
| accessor:acc DOT ID:d {:if(acc !=null) {
		acc.ids.add(d);
		RESULT = acc;
		}
		else
		{
                   RESULT = new AccessorNode(d);RESULT.setup(dleft,dright);}
:}
;

tuple ::=
LEFTTBRACKET expression_clist:elist RIGHTTBRACKET		{: RESULT = new TupleNode(elist);RESULT.setup(elistleft,elistright); :}
;

list ::= 
LEFTBRACKET expression_clist:elist RIGHTBRACKET 		{: RESULT = new ListNode(elist);RESULT.setup(elistleft,elistright); :}
;

expression_clist ::= 
expression_clist:elist COMMA expression:e
{:        
		if (elist == null) {
			//System.out.println("elist is NULL!");
			elist = new ExprListNode(e);	
		} else {
			elist.expressions.add(e);
		}
		RESULT = elist;
		:}
| 
expression:e	{: 
		if(e!=null)	
		 {
                   RESULT = new ExprListNode(e);RESULT.setup(eleft,eright); 
		}:}
| {:RESULT= new ExprListNode(null);:}
;



expression ::=
term:t				{: RESULT = t; :}
| NOT expression:e		{: RESULT = new NotExprNode(e);RESULT.setup(eleft,eright); :}
| expression:e PLUS term:t		{: RESULT = new PlusNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e MINUS term:t	{: RESULT = new MinusNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e OR term:t	{: RESULT = new OrNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e IN term:t	{: RESULT = new InNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e LESSTHAN term:t		{: RESULT = new LessThNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e LESSTHANEQUAL term:t	{: RESULT = new LessThEqNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e GREATERTHAN term:t		{: RESULT = new GreatThNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e GREATERTHANEQUAL term:t	{: RESULT = new GreatThEqNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e EQUAL term:t		{: RESULT = new EqNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e NOTEQUAL term:t		{: RESULT = new NotEqNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e CONCAT term:t		{: RESULT = new ConcatNode(e,t);RESULT.setup(eleft,eright); :}
| expression:e1 LEFTBRACKET expression:e2 COLON expression:e3 RIGHTBRACKET	{: RESULT = new ThreeExprNode(e1,e2,e3);RESULT.setup(e1left,e1right); :}
| expression:e1 LEFTBRACKET expression:e2 COLON RIGHTBRACKET		{: RESULT = new EndColNode(e1,e2);RESULT.setup(e1left,e1right); :}
| expression:e1 LEFTBRACKET COLON expression:e2 RIGHTBRACKET		{: RESULT = new BegColNode(e1,e2);RESULT.setup(e1left,e1right); :}
| expression:e1 LEFTBRACKET expression:e2 RIGHTBRACKET		{: RESULT = new NoColNode(e1,e2);RESULT.setup(e1left,e1right); :}
;

term ::=
factor:fac		{: RESULT = fac; :}
| term:trm MULTIPLY factor:fac		{: RESULT = new MultiplyNode(trm,fac);RESULT.setup(trmleft,trmright); :}
| term:trm DIVIDE factor:fac		{: RESULT = new DivideNode(trm,fac);RESULT.setup(trmleft,trmright); :}	
| term:trm POWER factor:fac		{: RESULT = new PowerTermNode(trm,fac);RESULT.setup(trmleft,trmright); :}
| term:trm AND factor:fac		{: RESULT = new AndNode(trm,fac);RESULT.setup(trmleft,trmright); :}
;

factor ::=
exponent:exp POWER factor:fac	{: RESULT = new PowerNode(exp,fac);RESULT.setup(facleft,facright); :}
| exponent:exp	{: RESULT = exp ;:}
;

exponent ::=
MINUS exponent:exp {: RESULT = new MinExponentNode(exp); :}
| final:fin {: RESULT = fin;:}
;

final ::=
accessor:acc	{: RESULT = acc; :}
| LEFTPAREN expression:e RIGHTPAREN  {: RESULT = new BrkExprNode(e);RESULT.setup(eleft,eright); :}
| INTLITERAL:val	{: RESULT = new IntNode(val);RESULT.setup(valleft,valright); :}
| FLOATLITERAL:val	{: RESULT = new FloatNode(val);RESULT.setup(valleft,valright); :}
| BOOLLITERAL:val	{: RESULT = new BoolNode(val);RESULT.setup(valleft,valright); :}
| CHARLITERAL:val	{: RESULT = new CharNode(val);RESULT.setup(valleft,valright); :}
| function_call:func	{: RESULT = func;:}
| LENGTH LEFTPAREN expression:e RIGHTPAREN	{: RESULT = new LengthNode(e);RESULT.setup(eleft,eright); :}
//| sequence:seq {: RESULT = seq;:}
| STRINGLITERAL:s   {: RESULT = new StringLitNode(s);RESULT.setup(sleft,sright); :}
| tuple	:tup		{: RESULT = tup;:}
| list:lis		{: RESULT = lis;:}
;
